#!/bin/zsh

zmodload zsh/zutil
autoload -Uz add-zsh-hook

function load-nix() {
    setopt local_options err_return

    if [[ -n "$NIX_SERVER_SOCKET" ]]; then
        printf 'A server is already running at %s\n' "$NIX_SERVER_SOCKET" >&2
        return 1
    fi

    local -a arg_forwarder arg_socket
    zparseopts -D \
        -forward:=arg_forwarder \
        -socket:=arg_socket

    local state_dir="${XDG_STATE_HOME-${HOME}/.local/state}/nix-server"
    local run_dir="${XDG_RUNTIME_DIR-${HOME}/.cache}/nix-server"
    mkdir -p "$state_dir"
    mkdir -p "$run_dir"
    chmod 0700 "$run_dir"

    typeset -gx NIX_SERVER_SOCKET
    if [[ -z $arg_socket ]]; then
        NIX_SERVER_SOCKET="$run_dir/$$.sock"
    else
        NIX_SERVER_SOCKET="$arg_socket[-1]"
    fi

    local log_file="$state_dir/$$.server.log"
    local server_pid_file="$state_dir/$$.server.pid"

    local plugin_dir="$(whence -p make-nix-forwarder)"
    plugin_dir="${plugin_dir:a:h}/.."
    command-server start \
        --pid-fd 3 \
        "$NIX_SERVER_SOCKET" \
        "$plugin_dir/server.conf" \
        --log-file "$log_file" \
        -- "$@" 3> "$server_pid_file"
    command-server reload "$NIX_SERVER_SOCKET"

    typeset -g NIX_SERVER_PID="$(cat "$server_pid_file")"
    printf 'Server running on pid %s\n' "$NIX_SERVER_PID"

    local forwarder
    if [[ -n $arg_forwarder ]]; then
        forwarder="$arg_forwarder[-1]"
        typeset -g NIX_SERVER_FORWARDING_PATH="${XDG_CONFIG_HOME-${HOME}/.config}/nix-server/bins/$forwarder"
        path=("$NIX_SERVER_FORWARDING_PATH" "$path[@]")
    fi

    add-zsh-hook zshexit unload-nix
}

load-nix "$@"
