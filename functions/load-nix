# vim: set syntax=zsh

zmodload zsh/zutil
autoload -Uz add-zsh-hook

function load-nix() {
    setopt local_options err_return

    if [[ -n "$NIX_SERVER_SOCKET" ]]; then
        printf 'A server is already running at %s\n' "$NIX_SERVER_SOCKET" >&2
        return 1
    fi

    local -a arg_forward_path arg_socket
    zparseopts -D -E -K \
        -forward:=arg_forward_path \
        -socket:=arg_socket

    local forward_path
    if [[ -n $arg_forward_path ]]; then
        forward_path="$arg_forward_path[-1]"
        typeset -g NIX_SERVER_FORWARDING_PATH="$arg_forward_path"
        path=("$forward_path" "$path[@]")
    fi

    local state_dir="${XDG_STATE_HOME-${HOME}/.local/state}/nix-server"
    local run_dir="${XDG_RUNTIME_DIR-${HOME}/.cache}/nix-server"
    mkdir -p "$state_dir"
    mkdir -p "$run_dir"
    chmod 0700 "$run_dir"

    typeset -gx NIX_SERVER_SOCKET
    if [[ -n $arg_socket ]]; then
        NIX_SERVER_SOCKET="$arg_socket[-1]"
    else
        NIX_SERVER_SOCKET="$run_dir/$$.sock"
    fi

    local log_file="$state_dir/$$.server.log"
    local server_pid_file="$state_dir/$$.server.pid"

    command-server-start \
        "$(whence -p make-nix-forwarder)/../server.conf" \
        --socket "$socket" \
        --log-file "$log_file" \
        -- "$@" 3> "$server_pid_file"

    typeset -g NIX_SERVER_PID="$(cat "$server_pid_file")"
    printf 'Server running on pid %s\n' "$NIX_SERVER_PID"

    add-zsh-hook zshexit unload-nix
}

load-nix "$@"
