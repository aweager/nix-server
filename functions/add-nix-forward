#!/bin/zsh

zmodload zsh/zutil

function add-nix-forward() {
    setopt local_options err_return

    local -a arg_dest
    zparseopts -D -E -K \
        {d,-dest}:=arg_dest

    local dest_dir

    if [[ -n $arg_dest ]]; then
        dest_dir="${XDG_CONFIG_HOME-${HOME}/.config}/nix-server/bins/$arg_dest[-1]"
    elif [[ -n "$NIX_SERVER_FORWARDING_PATH" ]]; then
        dest_dir="$NIX_SERVER_FORWARDING_PATH"
    else
        printf '%s\n' \
            "Usage:" \
            "    add-nix-forward [[-d | --dest] <destination>] command[:rename]..." \
            >&2
        return 1
    fi

    if [[ ! -x "${dest_dir}/impl/forwarder" ]]; then
        printf 'Forwarder does not exist at %s\n' "$dest_dir" >&2
        return 1
    fi

    local to_forward forward_name prog_name
    for to_forward; do
        if [[ "$to_forward" == *:* ]]; then
            forward_name="${to_forward%%:*}"
            prog_name="${to_forward#*:}"
            () {
                printf '#!/bin/zsh -f\n'
                printf 'exec "${0:a:h}/impl/forward-with-rename" %s "$@"' "${(q)prog_name}"
            } > "${dest_dir}/${forward_name}"
            chmod +x "${dest_dir}/${forward_name}"
        elif [[ ! -e "${dest_dir}/${to_forward}" ]]; then
            ln -s "impl/forwarder" "${dest_dir}/${to_forward}"
        fi
    done
}

add-nix-forward "$@"
