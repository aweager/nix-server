#!/bin/zsh

setopt err_exit

zmodload zsh/zutil

function print_usage() {
    printf '%s\n' \
        "Usage:" \
        "    make-nix-forwarder [-d | --dest] <destination> [[-S | --socket-var] <sock-var>]" \
        >&2

    exit 1
}

local -a arg_dest arg_sock_var
zparseopts -D -E -K \
    {d,-dest}:=arg_dest \
    {S,-socket-var}:=arg_sock_var

local dest dest_folder sock_var

if [[ -z $arg_dest ]]; then
    print_usage
fi
dest="$arg_dest[-1]"
dest_folder="${XDG_CONFIG_HOME-${HOME}/.config}/nix-server/bins/$dest"

if [[ -n $arg_sock_var ]]; then
    sock_var="$arg_sock_var[-1]"
else
    sock_var="NIX_SERVER_SOCKET"
fi

mkdir -p "${dest_folder}/impl"

echo "Creating forwarder script:\n"

() {
    printf '#!/bin/zsh -f\n'
    printf 'exec command-server run "$%s" "$(basename "$0")" "$@"\n' "$sock_var"
} >&1 > "${dest_folder}/impl/forwarder"

echo "Creating renaming forwarder script:\n"

() {
    printf '#!/bin/zsh -f\n'
    printf 'prog_name="$1"; shift\n'
    printf 'exec command-server run "$%s" "$prog_name" "$@"\n' "$sock_var"
} >&1 > "${dest_folder}/impl/forward-with-rename"

chmod +x "${dest_folder}/impl/forwarder"
chmod +x "${dest_folder}/impl/forward-with-rename"

echo "Forwarding setup created in ${dest_folder}"
